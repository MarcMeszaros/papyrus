// build requirements
buildscript {
    // build dependency repositories
    repositories {
        mavenCentral()
    }
    // build dependencies
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3' // android build tools
    }
}

// build plugins
apply from: 'git.gradle'
apply plugin: 'com.android.application'

// only import signing if available
if (file('signing.gradle').exists()) {
    apply from: 'signing.gradle'
}

// application dependency repositories
repositories {
    maven { url 'http://mavenrepo.google-api-java-client.googlecode.com/hg' } // google-api
    mavenCentral()
}

// application dependencies
dependencies {
    // Android support libraries
    compile 'com.android.support:cardview-v7:22.1.0'
    compile 'com.android.support:recyclerview-v7:22.1.0'
    compile 'com.android.support:support-v13:22.1.0'

    // google books SDKs
    compile 'com.google.apis:google-api-services-books:v1-rev70-1.20.0'
    compile 'com.google.http-client:google-http-client-jackson2:1.20.0'

    // Injection Library
    compile 'com.jakewharton:butterknife:6.1.0'

    // Debugging helper library
    compile 'com.jakewharton.timber:timber:3.0.1'
}

// version name
def getVersionName = { ->
    def versionTag = gitTag() ?: ''
    if (gitCurrentBranch() != 'master' || versionTag.length() < 1) {
        return gitHash() + gitDirty()
    } else {
        return versionTag
    }
}

// android build settings
android {
    compileSdkVersion 21
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode = gitCommitCount()
        versionName = getVersionName()
    }

    // http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Using-sourceCompatibility-1.7
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // build types
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFile 'proguard-rules.txt'
            applicationIdSuffix '.debug'
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFile 'proguard-rules.txt'
            if (file('signing.gradle').exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
}